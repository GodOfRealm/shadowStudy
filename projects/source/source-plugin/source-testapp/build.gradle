apply plugin: 'com.android.application'
apply plugin: 'com.tencent.shadow.plugin'
apply plugin: 'kotlin-android'

buildscript {
    repositories {
        if (!System.getenv().containsKey("DISABLE_TENCENT_MAVEN_MIRROR")) {
            maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
        } else {
            google()
            jcenter()
        }
    }

    dependencies {
        classpath 'com.tencent.shadow.core:runtime'
        classpath 'com.tencent.shadow.core:activity-container'
        classpath 'com.tencent.shadow.core:gradle-plugin'
        classpath 'org.javassist:javassist:3.28.0-GA'
    }
}

android {
    compileSdkVersion project.COMPILE_SDK_VERSION

    defaultConfig {
        applicationId 'com.tencent.shadow.sample.plugin.app'
        minSdkVersion project.MIN_SDK_VERSION
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode project.VERSION_CODE
        versionName project.VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.create("release")
            signingConfig.initWith(buildTypes.debug.signingConfig)
        }
    }

    // 将插件applicationId设置为和宿主相同
    productFlavors {
        plugin {
            applicationId project.SAMPLE_HOST_APP_APPLICATION_ID
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.core:core:1.3.2'

    //注意sample-host-lib要用compileOnly编译而不打包在插件中。在packagePlugin任务中配置hostWhiteList允许插件访问宿主的类。
    pluginCompileOnly project(":source-host-lib")
    normalImplementation project(":source-host-lib")

    pluginCompileOnly project(":source-base-lib")
    normalImplementation project(":source-base-lib")

    //Shadow Transform后业务代码会有一部分实际引用runtime中的类
    //如果不以compileOnly方式依赖，会导致其他Transform或者Proguard找不到这些类
    pluginCompileOnly 'com.tencent.shadow.core:runtime'
}

preBuild.dependsOn(":source-host-lib:jarDebugPackage")


def createDuplicateApkTask(buildType) {
    def apkDir = file("${getBuildDir()}/outputs/apk/plugin/$buildType")

    return tasks.create("duplicatePlugin${buildType.capitalize()}ApkTask", Copy) {
        group = 'build'
        description = "复制一个source-testapp-plugin-${buildType}.apk用于测试目的"
        from(apkDir) {
            include("source-testapp-plugin-${buildType}.apk")
            rename { "source-testapp-plugin-${buildType}2.apk" }
        }
        into(apkDir)

    }.dependsOn(":source-testapp:assemblePlugin${buildType.capitalize()}")
}

tasks.whenTaskAdded { task ->
    if (task.name == "assemblePluginDebug") {
        def createTask = createDuplicateApkTask('debug')
        task.finalizedBy(createTask)
    }
    if (task.name == "assemblePluginRelease") {
        def createTask = createDuplicateApkTask('release')
        task.finalizedBy(createTask)
    }
}


shadow {
    transform {
//        useHostContext = ['abc']
    }

    packagePlugin {
        pluginTypes {
            debug {
                loaderApkConfig = new Tuple2('source-loader-debug.apk', ':source-loader:assembleDebug')
                runtimeApkConfig = new Tuple2('source-runtime-debug.apk', ':source-runtime:assembleDebug')
                pluginApks {
                    pluginApk1 {
                        businessName = 'source-plugin-app'
                        partKey = 'source-plugin-app'
                        buildTask = ':source-testapp:assemblePluginDebug'
                        apkPath = 'projects/source/source-plugin/source-testapp/build/outputs/apk/plugin/debug/source-testapp-plugin-debug.apk'
                        dependsOn = ['source-base']
                    }
                    pluginApk5 {
                        businessName = 'androidx-cases'
                        partKey = 'test-plugin-androidx-cases'
                        buildTask = ':test-plugin-androidx-cases:assemblePluginDebug'
                        apkPath = 'projects/source/source-plugin/test-plugin-androidx-cases/build/outputs/apk/plugin/debug/test-plugin-androidx-cases-plugin-debug.apk'
                        hostWhiteList = ["com.demo.source.host.lib"]
                    }
                    pluginApk6 {
                        businessName = 'hello-app'
                        partKey = 'test-plugin-hello-app'
                        buildTask = ':test-plugin-hello-app:assemblePluginDebug'
                        apkPath = 'projects/source/source-plugin/test-plugin-hello-app/build/outputs/apk/plugin/debug/test-plugin-hello-app-plugin-debug.apk'
                        hostWhiteList = ["com.demo.source.host.lib"]
                    }
                    pluginApk7 {
                        businessName = 'mame-app'
                        partKey = 'test-plugin-mame-app'
                        buildTask = ':emulatormodule:assemblePluginDebug'
                        apkPath = 'projects/source/source-plugin/emulatormodule/build/outputs/apk/plugin/debug/emulatormodule-plugin-debug.apk'
                        hostWhiteList = ["com.demo.source.host.lib"]
                    }
                    sampleBase {
                        businessName = 'source-plugin-app'
                        partKey = 'source-base'
                        buildTask = ':source-base:assemblePluginDebug'
                        apkPath = 'projects/source/source-plugin/source-base/build/outputs/apk/plugin/debug/source-base-plugin-debug.apk'
                    }
                }
            }

            release {
                loaderApkConfig = new Tuple2('source-loader-release.apk', ':source-loader:assembleRelease')
                runtimeApkConfig = new Tuple2('source-runtime-release.apk', ':source-runtime:assembleRelease')
                pluginApks {
                    pluginApk1 {
                        businessName = 'source-plugin-app'
                        partKey = 'source-plugin-app'
                        buildTask = ':source-testapp:assemblePluginRelease'
                        apkPath = 'projects/source/source-plugin/source-testapp/build/outputs/apk/plugin/release/source-testapp-plugin-release.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['source-base']
                    }
                    pluginApk2 {
                        businessName = 'source-plugin-app2'
                        partKey = 'source-plugin-app2'
                        buildTask = ':source-testapp:assemblePluginRelease'
                        apkPath = 'projects/source/source-plugin/source-testapp/build/outputs/apk/plugin/release/source-testapp-plugin-release2.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['source-base']
                    }
                    sampleBase {
                        businessName = 'source-plugin-app'
                        partKey = 'source-base'
                        buildTask = ':source-base:assemblePluginRelease'
                        apkPath = 'projects/source/source-plugin/source-base/build/outputs/apk/plugin/release/source-base-plugin-release.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                    }
                }
            }
        }

        loaderApkProjectPath = 'projects/source/source-plugin/source-loader'
        runtimeApkProjectPath = 'projects/source/source-plugin/source-runtime'

        archiveSuffix = System.getenv("PluginSuffix") ?: ""
        archivePrefix = 'plugin'
        destinationDir = "${getRootProject().getBuildDir()}"

        version = 4
        compactVersion = [1, 2, 3]
        uuidNickName = "1.1.5"
        uuid = '1234567890'
    }
}